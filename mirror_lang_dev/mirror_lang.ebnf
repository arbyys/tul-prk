Grammar ::= Program

(* Character classes *)
WhitespaceChar ::= " " | "\t" | "\n" | "\r"
Whitespace ::= WhitespaceChar*
WS ::= Whitespace

Digit ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"
LowerLetter ::= "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
UpperLetter ::= "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
Letter ::= LowerLetter | UpperLetter
Underscore ::= "_"

(* Variables *)
VariableChar ::= LowerLetter | Digit | Underscore
VariableName ::= LowerLetter VariableChar*
Variable ::= "$" VariableName

(* Operators *)
ArithmeticOperator ::= "+" | "-" | "*" | "/" | "^" | "%"
CompareOperator ::= "==" | ">=" | ">" | "<=" | "<" | "!="

(* Values *)
IntValue ::= Digit+
StringValue ::= '"' (Letter | Digit | WhitespaceChar | "." | "," | ":" | "+" | "-" | "*" | "/" | "^" | "%" | "$")* '"'
Value ::= IntValue | StringValue | Variable

(* Conditions *)
Condition ::= Variable CompareOperator Variable

(* Comments *)
SingleLineComment ::= "//" (Letter | Digit | WhitespaceChar | "." | "," | ":" | "+" | "-" | "*" | "/" | "^" | "%" | "$")*
MultiLineComment ::= "(IGNORE" WS Program WS "ERONGI)"

(* Keywords *)
SetKeyword ::= "(SET" WS Variable WS "=" WS Value WS "TES)"
LoopIfKeyword ::= "(LOOPIF" WS Condition WS "\n" Program "FIPOOL)"
IfKeyword ::= "(IF" WS Condition WS "\n" Program "FI)"
LogVariableKeyword ::= "(LOG" WS Variable WS "GOL)"
LogStringKeyword ::= "(LOG" WS StringValue WS "GOL)"

(* Program structure *)
Keyword ::= SetKeyword | LoopIfKeyword | IfKeyword | LogVariableKeyword | LogStringKeyword | SingleLineComment | MultiLineComment
Program ::= (Keyword "\n")*
